plugins {
    id 'java'
    id 'application'
    id "com.google.protobuf" version "0.9.4"
}

group = 'org.example'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    // GRPC
    runtimeOnly 'io.grpc:grpc-netty-shaded:1.58.0'
    implementation 'io.grpc:grpc-protobuf:1.58.0'
    implementation 'io.grpc:grpc-stub:1.58.0'
    compileOnly 'org.apache.tomcat:annotations-api:6.0.53' // necessary for Java 9+
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.22.3"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.58.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

// Define a Gradle task for GreetingServer
tasks.register('runServer', JavaExec) {
    main = 'com.grpc.chatroom.GreetingServer'
    classpath = sourceSets.main.runtimeClasspath
}

// Define a Gradle task for GreetingClient
tasks.register('runClient', JavaExec) {
    main = 'com.grpc.chatroom.GreetingClient'
    classpath = sourceSets.main.runtimeClasspath
}
